import com.weareadaptive.hydraplatform.gradle.plugins.CucumberPlugin
import com.weareadaptive.hydraplatform.gradle.plugins.CucumberTask

apply plugin: 'java'
apply plugin: CucumberPlugin

dependencies {
    testImplementation "com.weareadaptive:hydra-platform-cucumber:${hydraVersion}"
    testImplementation "com.google.truth:truth:1.1.3"

    implementation project(":component:mycluster:api")
    implementation project(":component:mygateway:api")
    implementation project(":component:mycluster:impl")
    implementation project(":component:mygateway:impl")
}

def commonCucumberArgs = [
        '--plugin', 'json:build/reports/cucumber.json',
        '--plugin', 'junit:build/reports/cucumber.xml',
        '--plugin', 'pretty',
        'src/test/resources'
]

tasks.register('cucumberEmbeddedDirect', CucumberTask.class, { task ->
    task.cucumberArgs = commonCucumberArgs + [
            '--tags', System.getProperty('cucumber.tags', 'not @WorkInProgress and not @PerfTest and not @EmbeddedRealOnly'),
            '--threads', Runtime.getRuntime().availableProcessors()
    ] as List<String>
    task.cucumberSystemProperties = [
            'test.mode': System.getProperty('test.mode', 'embedded-direct')
    ]
})

tasks.register('cucumberEmbeddedReal', CucumberTask.class, { task ->
    task.cucumberArgs = commonCucumberArgs + [
            '--tags', System.getProperty('cucumber.tags', 'not @WorkInProgress and not @PerfTest and not @EmbeddedDirectOnly')
    ] as List<String>
    task.cucumberSystemProperties = [
            'test.mode': System.getProperty('test.mode', 'embedded-real')
    ]
})
