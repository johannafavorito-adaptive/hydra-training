import com.weareadaptive.hydraplatform.gradle.ArtifactoryCredentials
import com.weareadaptive.hydraplatform.gradle.plugins.DockerServicePlugin

buildscript {
    ext {
        hydraVersion = '4.66.0'
    }

    ext.adaptiveRepositories = {
        mavenLocal()
        gradlePluginPortal {
            content {
                excludeGroupByRegex "com\\.weareadaptive.*"
                excludeModule("io.aeron", "aeron-cluster-standby")
            }
        }
        maven {
            url 'https://weareadaptive.jfrog.io/artifactory/hydra-release/'
            credentials {
                def credentials = ArtifactoryCredentials.load(project)
                username credentials.username
                password credentials.password  // pragma: allowlist secret
            }
            content {
                includeGroup "com.weareadaptive"
                includeGroup "io.aeron"
            }
        }
    }
    repositories adaptiveRepositories
    dependencies {
        classpath "com.weareadaptive:hydra-code-gen-plugin:${hydraVersion}"
    }
}

plugins {
    id 'com.google.cloud.tools.jib' apply false
}

allprojects {
    repositories adaptiveRepositories
}

ext {
    // Please include the trailing slash
    dockerTargetRegistry = System.getenv().containsKey('PLATFORM_REGISTRY') ? System.getenv('PLATFORM_REGISTRY') : 'weareadaptive-hydra-docker-snapshot.jfrog.io/'
    // Override with -PdockerTag=...
    dockerTag = project.properties['dockerTag'] ?: 'latest'
    docker_cluster_addresses = "173.18.202.2"
    docker_services = [
            'my-cluster',
            'my-gateway'
    ] as Set
}

defaultTasks 'build', 'cucumberEmbeddedDirect', 'cucumberEmbeddedReal'

group 'org.adaptive'
version '1.0-SNAPSHOT'

repositories adaptiveRepositories

apply plugin: DockerServicePlugin
